PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <http://schema.org/>
PREFIX cube: <https://cube.link/>
PREFIX ssz: <https://ld.stadt-zuerich.ch/schema/>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  <urn:ssz:terms> a hydra:Collection ; hydra:member ?term .
  ?term rdfs:label ?label
}
WHERE {
  {
    SELECT ?cube WHERE {
      BIND ("{CUBE}" as ?cubes)
      VALUES ?n { 1 2 3 4 5 6 7 8 9 10 }

      BIND (concat("^([^,]*,){", str(?n) ,"} *") AS ?skipN)
      BIND (iri(replace(replace(?cubes, ?skipN, ""), ",.*$", "")) AS ?cube)
    }
  }

  VALUES ?dimension { <{DIMENSION}> }
  VALUES ?filter { <{FILTER}> }

  ?filter schema:hasDefinedTerm ?term .
  ?term schema:name ?label ;
        schema:position ?position .

  FILTER EXISTS {
    ?cube cube:observationSet/cube:observation ?observation .
    ?observation ?dimension ?term .
  }
}

PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cube: <https://cube.link/>

CONSTRUCT {
  <urn:ssz:measures> a hydra:Collection ; hydra:member ?sub .
  ?sub rdfs:label ?kennzahlLabel .
}
FROM <https://lindas.admin.ch/stadtzuerich/stat>
WHERE {
  SELECT DISTINCT ?sub ?kennzahlLabel {
    ?sub a cube:MeasureDimension ;
      schema:identifier ?kennzahl ;
      schema:name ?kennzahlLabel .

    # Limit only those whaich are used in cubes
    ?shape a cube:Constraint ;
      sh:property/sh:path ?sub .

    FILTER (
      CONTAINS(LCASE(?kennzahlLabel), LCASE("{Q}")) ||
      LCASE(?kennzahl) = LCASE("{Q}")
    )
  }
  LIMIT 10
}
